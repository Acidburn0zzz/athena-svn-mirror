dnl withenable.ac: --with and --enable options.

dnl The implementation of the defaults for the next three options is found
dnl in configure.in.

dnl Do we compile Omega?
dnl We default to yes if the sources are available.
AC_ARG_WITH(omega,
[  --without-omega            don't compile and install Omega])

dnl Do we compile PDFTeX?
dnl We default to yes if the sources are available.
AC_ARG_WITH(pdftex,
[  --without-pdftex           don't compile and install PDFTeX])

dnl Do we compile e-TeX?
dnl Here we have three choices: yes, no, and to use etex as tex.
dnl We default to yes if the sources are available.
AC_ARG_WITH(etex,
[  --with-etex=ETEX           ETEX=no  don't compile and install e-TeX
                             ETEX=tex install e-TeX as tex])

AC_ARG_WITH(system-zlib,
[  --with-system-zlib         use installed zlib library],,
with_system_zlib=no)

AC_ARG_WITH(system-pnglib,
[  --with-system-pnglib       use installed pnglib library],,
with_system_pnglib=no)

AC_ARG_WITH(mktextex-default,
[  --with-mktextex-default    run mktextex if TeX source missing],
  withval=yes, withval=no)
if test "x$withval" = xyes; then
  AC_DEFINE(MAKE_TEX_TEX_BY_DEFAULT)
fi

AC_ARG_WITH(editor, changequote(`,')dnl
`  --with-editor=CMD       invoke CMD from the e option [vi +%d %s]',
changequote([,])dnl
  AC_DEFINE_UNQUOTED(EDITOR, "$with_editor")
)

AC_ARG_ENABLE(ipc,
[  --enable-ipc            enable TeX's --ipc option, i.e., pipe to a program],
  AC_DEFINE(IPC)
  # -lsocket is needed on Solaris, at least.  Maybe -lnsl on SCO, too?
  # See ac_path_xtra.
  AC_CHECK_FUNC(connect)
  if test x$ac_cv_func_connect = xno; then
    AC_CHECK_LIB(socket, connect, socketlibs="-lsocket $socketlibs")
  fi
)
AC_SUBST(socketlibs)

AC_ARG_ENABLE(auto-core,
[  --enable-auto-core      cause TeX&MF to dump core given a certain filename],
  AC_DEFINE(FUNNY_CORE_DUMP))

AC_ARG_ENABLE(dump-share,
[  --disable-dump-share    make fmt/base/mem files architecture-dependent],
if test "x$enableval" = xno; then
  AC_DEFINE(NO_DUMP_SHARE)
fi
)

# Window system support for Metafont.
AC_ARG_WITH(epsfwin,
  [  --with-epsfwin          include EPSF pseudo-window support],
  AC_DEFINE(EPSFWIN))
AC_ARG_WITH(hp2627win,
  [  --with-hp2627win        include HP 2627 window support in Metafont],
  AC_DEFINE(HP2627WIN))
AC_ARG_WITH(mftalkwin,
  [  --with-mftalkwin        include mftalk (generic server) window support],
  AC_DEFINE(MFTALKWIN))
AC_ARG_WITH(nextwin,
  [  --with-nextwin          include NeXT window support],
  AC_DEFINE(NEXTWIN))
AC_ARG_WITH(regiswin,
  [  --with-regiswin         include Regis window support],
  AC_DEFINE(REGISWIN))
AC_ARG_WITH(suntoolswin,
  [  --with-suntoolswin      include old Suntools window support (this is not X)],
  wlibs="-lsuntool -lsunwindow -lpixrect $wlibs"
  AC_DEFINE(SUNWIN))
AC_ARG_WITH(tektronixwin,
  [  --with-tektronixwin     include Tektronix window support],
  AC_DEFINE(TEKTRONIXWIN))
AC_ARG_WITH(unitermwin,
  [  --with-unitermwin       include Uniterm window support],
  AC_DEFINE(UNITERMWIN))

dnl The second is apparently superfluous.
sinclude(../kpathsea/xt.ac)dnl for web2c/configure
dnl sinclude(kpathsea/xt.ac)dnl for top-level configure

AC_ARG_WITH(x11win,
  [  --with-x11win           same as --with-x],
  with_x="$withval")
AC_ARG_WITH(x11,
  [  --with-x11              same as --with-x],
  with_x="$withval")
