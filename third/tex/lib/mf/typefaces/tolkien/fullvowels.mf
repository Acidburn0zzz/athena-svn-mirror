% Copyright 1988 Michael Urban
% Everyone is granted permission to copy, modify and redistribute
% this file, provided that a currently-dated copy of this
% copyright notice is included in the copy.
% License is granted to produce fonts based on the information
% in this file, provided that such fonts are redistributed
% without charge to any third party.

% SCCS ID @(#)fullvowels.mf	1.1   10/1/86

beginchar ("a",shortwidth, xht#, 0); "The letter a (modus beleriandi)";
	begingroup;
	save x,y;
	pickup elvishpen;
	top y1= h; x1 = .75[leadgap, w];
	lft x2= leadgap; y2= .4h;
	bot y3= 0; x3=  .5[leadgap, w];
	y4= .2h; rt x4=w;
	draw z1..z2{down}..z3{right}..z4;
	endgroup;
endchar;

beginchar ("y", shortwidth, xht#, 1/2dep#); "The letter y (modus beleriandi)";
	begingroup;
	save x,y;
	pickup elvishpen;
	x1=leadgap; y1=.5h;
	x2 = 2/3[x1,w]; top y2=h;
	rt x3=w; y3=0;
	lft x4=leadgap; y4=-d;
	draw z1..z2{right}..z3..z4;
	endgroup;
endchar;


beginchar("o", shortwidth,xht#,0); "The letter o (anna ar taith)";
	pickup elvishpen;
	nutelco(w,shortstem, stemdepth);
	nuluva (bowpoint, leadgap, true);
	draw barpoint..barpoint+3thin*right;
endchar;

