% Copyright 1988 Michael Urban
% Everyone is granted permission to copy, modify and redistribute
% this file, provided that a currently-dated copy of this
% copyright notice is included in the copy.
% License is granted to produce fonts based on the information
% in this file, provided that such fonts are redistributed
% without charge to any third party.

% SCCS ID @(#)luvar.mf	1.1   10/1/86

def luva(expr startpt, endx, looped) = 
begingroup;
	save x,y;
	z5 = startpt;
	top y6=xht; x6=.5[x5,endx];
	rt x7=endx; y7=.47xht;
    if looped :
	bot y8 =0; x8=1/4[x6,endx];
	y9 = .7thick; z9=whatever*(dir (tilt+90))+z8;
	draw z5..z6..z7{down}..z8{left}..z9;   % lu'va (bow)
    else:
	bot y8 =0; x8=.7[x6,endx];
	draw z5..z6..z7{down}..z8;%{dir (tilt+180)};
    fi;
	bowpoint:= z7;
	hookpoint := z8;
	penlabels(5,6,7,8,9);
endgroup;
enddef;

def nuluva(expr startpt, endx, looped) = 
begingroup;
	save x,y;
	z5 = startpt;
	bot y6=0; x6=.5[x5,endx];
	lft x7=endx; y7=.45xht;
    if looped :
	top y8 =xht; x8=1/4[x6,endx];
	y9 = xht-.7thick; z9=whatever*(dir (tilt-90))+z8;
	draw z5..z6..z7{up}..z8{right}..z9;   % lu'va (bow)
    else:
	top y8 =xht; x8=.7[x6,endx];
	draw z5..z6..z7{up}..z8;%{dir (tilt)};
    fi;
	bowpoint:= z7;
	hookpoint := z8;
	penlabels(5,6,7,8,9);
endgroup;
enddef;

