AC_INIT(src/netstatus-applet.c)

AM_INIT_AUTOMAKE(gnome-netstatus, 2.8.0)
AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

GETTEXT_PACKAGE=gnome-netstatus
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [gettext package])

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AC_ARG_PROGRAM
AM_PROG_LIBTOOL
AC_PATH_PROGS(PERL, perl5 perl)
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)
AC_PATH_PROG(GDK_PIXBUF_CSOURCE, gdk-pixbuf-csource, no)
AC_PATH_PROG(GCONFTOOL, gconftool-2)
AC_PROG_INTLTOOL

dnl make sure we keep ACLOCAL_FLAGS around for maintainer builds to work
AC_SUBST(ACLOCAL_AMFLAGS, "$ACLOCAL_FLAGS")

ALL_LINGUAS="ar az bg bn bs ca cs cy da de el en_CA en_GB es eu fi fr ga gu hi hr hu id it ja ko lt mn ms nb nl nn no pa pl pt pt_BR ro ru sk sq sr sr@Latn sv ta th tr vi uk wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

GNOME_COMPILE_WARNINGS(yes)

AC_ARG_ENABLE(deprecations,
              [AC_HELP_STRING([--enable-deprecations],
                              [warn about deprecated usages [default=yes]])],,
              [enable_deprecations=yes])

if test "x$enable_deprecations" = "xyes"; then
   DISABLE_DEPRECATED_CFLAGS="\
-DG_DISABLE_DEPRECATED \
-DGDK_DISABLE_DEPRECATED \
-DGTK_DISABLE_DEPRECATED \
-DGDK_PIXBUF_DISABLE_DEPRECATED \
-DGNOME_DISABLE_DEPRECATED"
   AC_SUBST(DISABLE_DEPRECATED_CFLAGS)
fi

PKG_CHECK_MODULES(NETSTATUS, libgnomeui-2.0 >= 2.5.2
			     libglade-2.0 >= 2.0.0
			     libpanelapplet-2.0 >= 2.0.0
			     gtk+-2.0 >= 2.3.1)
AC_SUBST(NETSTATUS_LIBS)
AC_SUBST(NETSTATUS_CFLAGS)
AC_CHECK_HEADERS(sys/sockio.h)

# Blatantly stolen from configure.in in the sample code from
# "UNIX Network Programming, Volume 1" by W. Richard Stevens
AC_CACHE_CHECK(if sockaddr has sa_len member,
ac_cv_sockaddr_has_sa_len,
        AC_TRY_COMPILE([
#               include <sys/types.h>
#               include <sys/socket.h>],
                [unsigned int i = sizeof(((struct sockaddr *)0)->sa_len)],
        ac_cv_sockaddr_has_sa_len=yes,
        ac_cv_sockaddr_has_sa_len=no))
if test x"$ac_cv_sockaddr_has_sa_len" = "xyes" ; then
        AC_DEFINE(HAVE_SOCKADDR_SA_LEN, 1,
                  [Defined when sockaddr has an sa_len member])
fi


dnl --enable-debug=(yes|minimum|no)
AC_ARG_ENABLE(debug, [  --enable-debug=[no/yes] turn on debugging [default=no]],,enable_debug=minimum)
if test "$enable_debug" = "yes"; then
  NETSTATUS_DEBUG_CFLAGS="-DG_ENABLE_DEBUG"
else
  if test "x$enable_debug" = "xno"; then
    NETSTATUS_DEBUG_CFLAGS="-DG_DISABLE_ASSERT -DG_DISABLE_CHECKS"
  else
    NETSTATUS_DEBUG_CFLAGS=""
  fi
fi
AC_SUBST(NETSTATUS_DEBUG_CFLAGS)

dnl
dnl     whether or not to run glib-mkenums etc.
dnl
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
   test -n "$PERL" && \
   $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 && \
   test -n "$AWK" ; then
	REBUILD=
fi
AC_SUBST(REBUILD)

AM_GCONF_SOURCE_2

AC_OUTPUT([
Makefile
po/Makefile.in
src/Makefile
icons/Makefile
help/Makefile
help/C/Makefile
help/de/Makefile
help/es/Makefile
help/fr/Makefile
help/it/Makefile
help/sv/Makefile
help/ja/Makefile
help/ko/Makefile
help/zh_CN/Makefile
help/zh_HK/Makefile
help/zh_TW/Makefile
packaging/Makefile
packaging/gnome-netstatus.spec
])
