dnl Process this file with autoconf to produce a configure script.
AC_INIT(afs2k5db.c)
AC_REVISION([$Id: configure.in,v 1.3 2003-02-24 18:04:08 zacheiss Exp $])
dnl
dnl Allow the user to specify the compiler and/or linker
dnl
CUSTOM_CONFIG
dnl
dnl Get the prefix from the location of "kinit"
dnl
AC_PREFIX_PROGRAM(kinit)
dnl
dnl Check for the right install program
dnl
AC_PROG_INSTALL
dnl
dnl Try to find krb5 include and library files
dnl
AC_ARG_WITH(krb5, [  --with-krb5=PATH        Location of Kerberos 5 libraries and include files])
dnl
krb5lib=${prefix}/lib
krb5inc=${prefix}/include
dnl
dnl Optional support for AFS
dnl
AC_ARG_WITH(afs,
[  --with-afs=AFSDIR       use preinstalled AFS library tree], ,with_afs=/usr/afsws)dnl
if test $with_afs != no; then
	AC_DEFINE(AFS)
	AFSLIBS="-L$with_afs/lib -L$with_afs/lib/afs -lsys -lprot -lubik -lauth -lrxkad -lrx -llwp -ldes -lsys $with_afs/lib/afs/util.a"
	CPPFLAGS="$CPPFLAGS -I$with_afs/include"
else
	echo "Cannot build without AFS tree"
	exit 1
fi

dnl
dnl See if we have getDirPath in util.a
dnl
AC_CACHE_CHECK([for getDirPath in $with_afs/lib/afs/util.a],
afs_cv_func_getDirPath,
[ac_save_LIBS="$LIBS"
LIBS="$LIBS $with_afs/lib/afs/util.a"
AC_TRY_LINK_FUNC(getDirPath, afs_cv_func_getDirPath=yes, afs_cv_func_getDirPath=no)
LIBS="$ac_save_LIBS"])
if test "x$afs_cv_func_getDirPath" = "xyes"; then
	AC_MSG_RESULT([Setting compilation parameters for AFS 3.5])
	LIBOBJS="$LIBOBJS adderrtable.o"
else
	AC_MSG_RESULT([Setting compilation parameters for pre-AFS 3.5])
	AC_DEFINE(PRE_AFS35)
fi

dnl
dnl Allow disabling of prdb code - for HP-UX 9.05 problems
dnl
AC_ARG_WITH(prdb,
[  --without-prdb          disable all AFS principal database calls],
[
if test "x$withval" = xno; then
	AC_DEFINE(FORCE_NOPRDB)
fi
],)

dnl
dnl Might need to specify location of source and build tree
dnl
AC_ARG_WITH(krb5-src,
[  --with-krb5-src=DIR     Location of Kerberos source tree],
[	EXTRA_INC="$EXTRA_INC -I$with_krb5_src/include -I$with_krb5_src/include/krb5"
	if test -r $with_krb5_src/lib/libdb.a ; then
		EXTRA_DB_LIB=$with_krb5_src/lib/libdb.a
	fi])
dnl
AC_ARG_WITH(krb5-obj,
[  --with-krb5-obj=DIR     Location of Kerberos build tree],
[	EXTRA_INC="$EXTRA_INC -I$with_krb5_obj/include -I$with_krb5_obj/include/krb5"
	EXTRA_DB_LIB=$with_krb5_obj/lib/libdb.a])
dnl
dnl Check to see if we need "netlibs" (specifically, libnsl and libsocket)
dnl
uname=`(uname) 2>/dev/null`
if test "$uname" != IRIX -a "$uname" != IRIX64 ; then
	AC_CHECK_LIB(socket, socket,
[	SYSLIBS="$SYSLIBS -lsocket"], ,-lnsl)
	AC_CHECK_LIB(nsl, t_bind,
[	SYSLIBS="$SYSLIBS -lnsl"])
fi
dnl
dnl See if we need a special library to handle regular expression stuff
dnl
save_LIBS="$LIBS"
LIBS=-lgen
AC_CHECK_FUNCS(compile step)
if test "$ac_cv_func_compile" = yes ; then
	REGEXP_LIB=-lgen
fi
LIBS="$save_LIBS"
dnl
dnl Sigh, we need to look for the 524 lib first since we can't specify it in the
dnl regular LIBS variable
dnl
ac_push_LIBS="$LIBS"
LIBS=""
AC_FIND_LIB(krb524, krb524_convert_creds_kdc, ${krb5lib} /usr/krb5/lib /usr/local/lib, ,[
	echo "Cannot find 524 library, exiting"
	exit 1
], -lkrb5 -lk5crypto -lcom_err $SYSLIBS)
KRB524LIB="$LIBS"
LIBS="$ac_push_LIBS"
ac_push_LIBS="$LIBS"
LIBS=""
AC_FIND_LIB(kdb5, krb5_db_fetch_mkey, ${krb5lib} /usr/krb5/lib /usr/local/lib, ,[
	echo "Cannot find Kerberos 5 DB library, will not be able to build DB utilities"
], -lkrb5 -lk5crypto -lcom_err $SYSLIBS)
AC_FIND_LIB(kadm5clnt, krb5_read_realm_params, , ,[
	echo "Cannot find Kerberos 5 admin library, will not be able to build DB utilities"
], -lkrb5 -lk5crypto -lcom_err $SYSLIBS)
KD_LIBS="$LIBS"
LIBS="$ac_push_LIBS"
dnl We're assuming that krb5, crypto, and com_err are in the same place,
dnl that's why we're not specifying search paths for them
dnl
AC_FIND_LIB(krb5, krb5_init_context, ${krb5lib} /usr/krb5/lib /usr/local/lib, , [
	echo "Cannot find Kerberos 5 libraries, exiting"
	exit 1
], -lk5crypto -lcom_err $SYSLIBS)
AC_FIND_LIB(k5crypto, krb5_string_to_key, , ,[
	echo "Cannot find crypto library, exiting"
	exit 1
], -lcom_err $SYSLIBS)
AC_FIND_LIB(com_err, com_err, , ,[
	echo "Cannot find com_err library, exiting"
	exit 1
], $SYSLIBS)
dnl
dnl Check the system type to figure out which runtime-path flags we need
dnl to set
dnl
AC_CANONICAL_SYSTEM
changequote(<<, >>)dnl
case "$target" in
	*-sun-solaris*|i386-unknown-solaris*)
		LDPATH_FLAGS=`echo $LIBS | sed -e 's/-l[^ ]* *//g' -e 's/-L/-R/g'`
		if test $with_afs != no; then
			AFSLIBS="$AFSLIBS -lc -L/usr/ucblib -lucb -R/usr/ucblib"
		fi

		if test "$target" = "sparc-sun-solaris2.4" ; then
			# Solaris 2.4 needs libucb.a for bcopy() and friends.
			SYSLIBS="$SYSLIBS -L/usr/ucblib -lucb"
		fi
		;;
	*-hp-hpux*)
		LDPATH_FLAGS="-Wl,+b,:"
		if test $with_afs != no; then
			AFSLIBS="$AFSLIBS -lBSD"
		fi
		;;
	*-*-netbsd*)
		if test $with_afs != no; then
			AFSLIBS="$AFSLIBS -lcompat"
		fi
		;;
	*)
		LDPATH_FLAGS=
	;;
esac
changequote([, ])dnl
dnl
dnl Check for res_search
dnl
save_LIBS="$LIBS"
AC_CHECK_FUNCS(res_search)
if test "$ac_cv_func_res_search" = no; then
      for lib in dns nsl resolv; do
        if test "$HAVE_RES_SEARCH" != 1; then
          AC_CHECK_LIB(${lib}, res_search, LIBS="$LIBS -l$lib";HAVE_RES_SEARCH=1;AC_DEFINE(HAVE_RES_SEARCH))
        fi
      done    
      if test "$HAVE_RES_SEARCH" = 1; then
        AFSLIBS="$AFSLIBS -l$lib"
      fi
fi    
LIBS="$save_LIBS"
dnl
dnl Check for Kerberos 5 include files
dnl
AC_FIND_HEADER(krb5.h, $krb5inc /usr/krb5/include , ,
[
	echo "Cannot find Kerberos 5 include files, exiting"
	exit 1
])
dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(stdlib.h)
AC_CHECK_HEADERS(memory.h)
AC_CHECK_HEADERS(paths.h)
AC_CHECK_HEADERS(malloc.h)
dnl
dnl Check for functions
dnl
AC_CHECK_FUNCS(strerror)
dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIGNAL
AC_TYPE_PID_T
dnl Check for functions that we would need to replace
dnl output necessary symbols
AC_SUBST(CC)
AC_SUBST(LDPATH_FLAGS)
AC_SUBST(AFSLIBS)
AC_SUBST(SYSLIBS)
AC_SUBST(EXTRA_INC)
AC_SUBST(EXTRA_DB_LIB)
AC_SUBST(KRB524LIB)
AC_SUBST(KDB5LIB)
AC_SUBST(KD_LIBS)
AC_SUBST(REGEXP_LIB)
AC_SUBST(prefix)
AC_SUBST(LIBOBJS)
AC_OUTPUT(Makefile)
