AC_INIT(gnome-font-install.c)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-print,0.25)
AM_ACLOCAL_INCLUDE(macros)

dnl libtool versioning

dnl increment if the interface has additions, changes, removals.
GNOMEPRINT_CURRENT=13

dnl increment any time the source changes; set to 
dnl  0 if you increment CURRENT
GNOMEPRINT_REVISION=4

dnl increment if any interfaces have been added; set to 0
dnl  if any interfaces have been removed. removal has 
dnl  precedence over adding, so set to 0 if both happened.
GNOMEPRINT_AGE=2

AC_SUBST(GNOMEPRINT_CURRENT)
AC_SUBST(GNOMEPRINT_REVISION)
AC_SUBST(GNOMEPRINT_AGE)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CPP
AM_PROG_LIBTOOL
ALL_LINGUAS="ca da de el es et fi fr gl hr hu it ja ko lt nl no pl pt_BR ro ru sk sl sv tr uk zh_CN.GB2312"

GNOME_INIT

GNOME_COMPILE_WARNINGS
GNOME_X_CHECKS
GNOME_XML_CHECK
AM_GNOME_GETTEXT

AM_PATH_GDK_PIXBUF(0.7.0, , AC_MSG_ERROR([Need gdk-pixbuf-0.7.0 or later!]))

#dnl
#dnl A quick check for unicode support.
#dnl
#vers=`gnome-config --modversion unicode | sed -e "s/unicode-//" | \
#	awk 'BEGIN { FS = "."; } { printf "%d", $1 * 1000 + $2;}'`
#if test "$vers" -ge 4; then
#	AC_MSG_RESULT(found)
#else
#	AC_MSG_CHECKING(for libunicode >= 0.4 vi unicode-config)
#	vers=`unicode-config --version | \
#		awk 'BEGIN { FS = "."; } { printf "%d", $1 * 1000 + $2;}'`
#	if test "$vers" -ge 4; then
#		AC_MSG_RESULT(found)
#	else
#		AC_MSG_ERROR(not found)
#	fi
#fi

dnl -------------------------------
dnl Checks for zlib library.
dnl -------------------------------
_cppflags="${CPPFLAGS}"
_ldflags="${LDFLAGS}"

AC_ARG_WITH(zlib,
[  --with-zlib[=DIR]       use libz in DIR],[
  if test "$withval" != "no"; then
    Z_DIR=$withval
    CPPFLAGS="${CPPFLAGS} -I$withval/include"
    LDFLAGS="${LDFLAGS} -L$withval/lib"
  fi
])

AC_CHECK_HEADERS(zlib.h,
    AC_CHECK_LIB(z, gzread,[
	AC_DEFINE(HAVE_LIBZ)
        if test "x${Z_DIR}" != "x"; then
            Z_CFLAGS="-I${Z_DIR}/include"
            Z_LIBS="-L${Z_DIR}/lib -lz"
        else
            Z_LIBS="-lz"
        fi]))
AC_SUBST(Z_CFLAGS)
AC_SUBST(Z_LIBS)
dnl ------------- end ------------------



dnl
dnl Gtk-doc documentation
dnl
AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])
if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi
AC_SUBST(HTML_DIR)
                                                    
PRINT_LIBDIR='-L${libdir}'
PRINT_INCLUDEDIR='-I${includedir}'
PRINT_INCLUDEDIR="$PRINT_INCLUDEDIR `gnome-config --cflags gnomeui`"
PRINT_INCLUDEDIR="$PRINT_INCLUDEDIR `xml-config --cflags`"
PRINT_LIBS="-lgnomeprint `gnome-config --libs gnomeui gdk_pixbuf`"
PRINT_LIBS="$PRINT_LIBS `xml-config --libs`"

echo "FIXME FIXME FIXME -- this is terribly wrong!"
dnl
dnl The is completely contrary to the autoconf/libtool way of doing
dnl things.  If solaris needs that option, then libtool should have
dnl put it there.  It failed to do that for whoever caused this test
dnl to end up here, but the cause should be fixed -- not patched
dnl with duct tape like this.
dnl
AC_CANONICAL_HOST
case $host in
*-*-solaris*)
	PRINT_LIBDIR="${PRINT_LIBDIR} -R${libdir}"
	;;
esac

AC_SUBST(PRINT_LIBDIR)
AC_SUBST(PRINT_INCLUDEDIR)
AC_SUBST(PRINT_LIBS)

dnl
dnl For building tests within gnome-print
dnl
PRINT_TEST_CFLAGS=`$GNOME_CONFIG --cflags gnomeui gdk_pixbuf`
PRINT_TEST_CFLAGS="$PRINT_TEST_CFLAGS `xml-config --cflags`"
PRINT_TEST_LIBS=`$GNOME_CONFIG --libs gnomeui gdk_pixbuf`
PRINT_TEST_LIBS="$PRINT_TEST_LIBS `xml-config --libs`"

AC_SUBST(PRINT_TEST_CFLAGS)
AC_SUBST(PRINT_TEST_LIBS)

AC_PATH_PROGS(PERL, perl5 perl)
AC_SUBST(PERL)

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

AC_OUTPUT([
Makefile
doc/Makefile
fonts/Makefile
gnome-print.spec
profiles/Makefile
libgnomeprint/Makefile
po/Makefile.in
intl/Makefile
afms/Makefile
afms/adobe/Makefile
fonts/Makefile
tests/Makefile],
[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])

