-*- mode: m4 -*-
AC_PREREQ(2.52)
AC_INIT(libgnomeprintui, 2.2.1.1,
	http://bugzilla.gnome.org/enter_bug.cgi?product=gnome-print)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libgnomeprintui,2.2.1.1)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_STDC_HEADERS
AM_PROG_LIBTOOL

AC_DEFINE(WE_ARE_LIBGNOMEPRINT_INTERNALS, 1, [Define it so that we can read libgnomprint's private headers])

dnl =============
dnl Warning flags
dnl =============
GNOME_COMPILE_WARNINGS(maximum)
CFLAGS="$CFLAGS $warning_flags"

dnl =======
dnl gettext
dnl =======
GETTEXT_PACKAGE=libgnomeprintui-2.2
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Gettext package])

ALL_LINGUAS="am ar az be bg cs da de ca el es et eu fi fr ga gl he hi hr hu it ja ko lt lv mn ms nl nn no pl pt pt_BR ro ru sk sl sv ta tr uk vi wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

dnl ==========================================
dnl Checks for gtk-doc and docbook-tools
dnl ==========================================
AC_ARG_WITH(html-dir, [  --with-html-dir=PATH    path to installed docs])
if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi
AC_SUBST(HTML_DIR)

AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)
gtk_doc_min_version=0.9
if $GTKDOC ; then
    gtk_doc_version=`gtkdoc-mkdb --version`
    AC_MSG_CHECKING([gtk-doc version ($gtk_doc_version) >= $gtk_doc_min_version])

    IFS="${IFS= 	}"; gtk_save_IFS="$IFS"; IFS="."
    set $gtk_doc_version
    for min in $gtk_doc_min_version ; do
        cur=$1; shift
        if test -z $min ; then break; fi
        if test -z $cur ; then GTKDOC=false; break; fi
        if test $cur -gt $min ; then break ; fi
        if test $cur -lt $min ; then GTKDOC=false; break ; fi
    done
    IFS="$gtk_save_IFS"

    if $GTKDOC ; then
      AC_MSG_RESULT(yes)
    else
      AC_MSG_RESULT(no)
    fi
fi

AM_CONDITIONAL(HAVE_GTK_DOC, $GTKDOC)
AC_SUBST(HAVE_GTK_DOC)

AC_CHECK_PROG(DB2HTML, db2html, true, false)
AM_CONDITIONAL(HAVE_DOCBOOK, $DB2HTML)

dnl Let people disable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc        use gtk-doc to build documentation [default=auto]], enable_gtk_doc="$enableval", enable_gtk_doc=no)

if test x$enable_gtk_doc = xyes ; then
  if test x$GTKDOC = xtrue ; then
    enable_gtk_doc=yes
  else
    enable_gtk_doc=no 
  fi
fi

dnl NOTE: We need to use a separate automake conditional for this
dnl       to make this work with the tarballs.
AM_CONDITIONAL(ENABLE_GTK_DOC, test x$enable_gtk_doc = xyes)
dnl ==========================================
dnl END: Checks for gtk-doc and docbook-tools
dnl ==========================================

dnl =================
dnl pkg-config checks
dnl =================
PKG_CHECK_MODULES(LIBGNOMEPRINTUI,
	libgnomeprint-2.2 >= 2.2.1.1 \
	libgnomecanvas-2.0 >= 1.117.0)
AC_SUBST(LIBGNOMEPRINTUI_CFLAGS)
AC_SUBST(LIBGNOMEPRINTUI_LIBS)

AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

dnl ================
dnl CFLAGS & LDFLAGS
dnl ================
AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl ================================================
dnl Used for the examples that need libglade support
dnl ================================================
PKG_CHECK_MODULES(LIBGLADE, [libglade-2.0], [libglade_msg=yes], [libglade_msg=no], [foo=yes])
AM_CONDITIONAL(WITH_LIBGLADE, test x"$libglade_msg" = "xyes")
AC_SUBST(LIBGLADE_CFLAGS)
AC_SUBST(LIBGLADE_LIBS)

dnl ============================================================================
dnl We might want to link the examples with 
dnl ../libgnomeprint/libgnomeprint/libgnomeprint-2-2.la to not have to make
dnl install after modifying libgnomeprint
dnl ============================================================================
AC_CHECK_FILE(../libgnomeprint/libgnomeprint/libgnomeprint-2-2.la, use_local_libgnomeprint_la="yes", use_local_libgnomeprint_la="no")
AM_CONDITIONAL(EXAMPLES_USE_LOCAL_LIBGNOMEPRINT, test "x$use_local_libgnomeprint_la" = "xyes")

AC_OUTPUT([
Makefile
po/Makefile.in
libgnomeprintui/Makefile
libgnomeprintui/gpaui/Makefile
libgnomeprintui/libgnomeprintui-2.2.pc
tests/Makefile
examples/Makefile
doc/Makefile
])

echo "
	Compiler:		${CC}
	Compiler flags:		${CFLAGS}"
if test "x$use_local_libgnomeprint_la" = "xyes" ; then
   echo "	Use local libgnomeprint-2-2.la for examples : Yes"
else
   echo "	Use local libgnomeprint-2-2.la for examples : No"
fi
if test "x$set_more_warnings" = "xno" ; then
   echo "	Disable more warnings:  Yes"
else
   echo "	Disable more warnings:  No"
fi
if test x$enable_gtk_doc = xyes ; then
   echo "	Enable gtk-doc:		Yes"
else
   echo "	Enable gtk-doc:		No"
fi
echo
