/* -*- Mode: IDL; tab-width: 8; indent-tabs-mode: 8; c-basic-offset: 8 -*- */

#include <Bonobo.idl>

module GNOME {
module Trilobite {
	module Eazel {

		/* NOTE: adding enums here requires adding them in
		   eazel-install-corba-types.c 
		    - corba_packagedatastruct_from_packagedata
		    - packagedata_from_corba_packagedatastruct
		   eazel-install-types.c
		    - packagedata_status_enum_to_str
		    - packagedata_status_str_to_enum
		*/
		   
		enum PackageStatusEnum {
			UNKNOWN_STATUS,
			SOURCE_NOT_SUPPORTED, /* bad: we don't install source packages */
			DEPENDENCY_FAIL,      /* bad: a dependency failed in next level */
			FILE_CONFLICT,        /* bad: this file owns a file that the
						 package we're installing also wants to install */
			BREAKS_DEPENDENCY,    /* bad: installing this breaks something, see PackageDataStruct.breaks */
			INVALID,              /* bad: this is not a rpm, go away */
			CANNOT_OPEN,          /* bad: cannot open/fetch/imagine this file */
			PARTLY_RESOLVED,      /* perhaps: the immediate deps where ok, check all in PackageDataStruct.soft/hard_depends */
			RESOLVED,             /* good: all deps ok */
			ALREADY_INSTALLED,    /* medium: you don't get to install it, but then again... */
			CIRCULAR_DEPENDENCY   /* bad: two packages are fighting it out */
		};
		
		enum ProtocolEnum {
			PROTOCOL_HTTP,
			PROTOCOL_FTP,
			PROTOCOL_LOCAL
		};

		struct DistributionStruct {
			string name;
			long major;
			long minor;
		};

		exception NoAccess {};         /* Raised if access to the package system wasn't allowed */
		exception PackagesFailed {};   /* Raised if some packages failed (un)install */

		struct PackageDataStruct {
			/*
			  These fields should be set when passed to 
			   Eazel:Install::install () 
			*/
			string name;
			string version;
			/* or set this you want to install a local file and
			   still use eg. protocol HTTP to do dep stuff */
			string filename;
			/*
			  Or this if you want to use a eazel package id */
			string eazel_id;

			/*
			  Eazel:Install::install ()
			   will set these if not set.
			*/
			string archtype;
			DistributionStruct distribution;

			/*
			  Not needed for 
			  Eazel:Install::install (), but will
			  (if available) be set when callbacks are called.
			 */
			string release;
			string summary;
			string description;
			long bytesize;
			boolean toplevel;

			/* The location the rpm is installed (if installed) */
			string install_root;

			/* The md5 checksum of the rpmfile */
			string md5;

			/* When Eazel::IntallCallback::install_failed is
			   called, this is set. The toplevel will have
			   PARTLY_RESOLVED and in soft_ & hard_depends,
			   packages will have typically eiter
			   RESOLVED for the good deps, 
			   bad deps will have one of the
			   bad: deps.
			*/
			PackageStatusEnum status;

			/* FIXME bugzilla.eazel.com 1542:
			   This is legal IDL, but not supported by wannabe idl compiler
			   orbit-idl. Therefore I have to encode these
			   trees as xml...
			   When orbit-idl parses IDL, we can loose the xml.
			sequence <PackageDataStruct> soft_depends;
			sequence <PackageDataStruct> hard_depends;
			sequence <PackageDataStruct> breaks;
			*/	
			sequence <string> provides;
		};		
		typedef sequence <PackageDataStruct> PackageDataStructList;

		struct CategoryStruct {
			string name;
			PackageDataStructList packages;
		};
		typedef sequence <CategoryStruct> CategoryStructList;

		interface InstallCallback : Bonobo::Unknown {
			/* Called during download of a file */
			/* make it 2-way so that it will process incoming corba calls */
			void download_progress (in PackageDataStruct package, in long amount, in long total);

			/* Called when dependency check is being handled */
			oneway void dependency_check (in PackageDataStruct package, in PackageDataStruct needs);

			/* Called when a package is being file conflict checked */
			/* FIXME: bugzilla.eazel.com 3459 */
			oneway void file_conflict_check (in PackageDataStruct package);

			/* Called after download is complete, and before the (un)install begins.
			   Typically while heating up RPM */
			boolean preflight_check (in string xml_packages,
						 in long total_size, 
						 in long num_packages);
			/* should be "in PackageDataStructList packages"
			   instead of a string, 
			   FIXME: bugzilla.eazel.com 1542 */
			
			/* Called during (un)installation of a package */
			oneway void install_progress (in PackageDataStruct package, 
							in long package_num, in long num_packages, 
							in long package_size_completed, in long package_size_total,
							in long total_size_completed, in long total_size);
			oneway void uninstall_progress (in PackageDataStruct package, 
							in long amount, in long total);

			/* Called whenever a package (un)install fails
			*/
			void download_failed (in PackageDataStruct package);
			/* FIXME bugzilla.eazel.com 1542:
			   This is what they should be, but orbit-idl's lack of
			   recursive structures, forces us to encode the structures
			   as xml and transfer these instead. 
			void install_failed (in PackageDataStruct package);
			void uninstall_failed (in PackageDataStruct package);
			*/
			void install_failed (in string xml_package);
			void uninstall_failed (in string xml_package);

			/* Emitted when a package fails the md5 check.
			   The package structure contains the md5 it should have,
			   and actual_md5 contains the md5 checksum of the rpmfile */
			void md5_check_failed (in PackageDataStruct package, in string actual_md5);

			/* Called to find out whether to delete the rpms or not, after finishing */
			boolean delete_files ();

			/* Called when the operation is finished */
			void done (in boolean result);
		};

		interface Install : Bonobo::Unknown {
			attribute boolean debug;
			attribute boolean verbose;     /* Mucho log output */
			attribute boolean silent;      /* Minimal log output */
			attribute boolean test_mode;   /* dry run, do not install, but act as if... */
			attribute boolean force;       /* force install specified package (DANGER WILL ROBINSON) */
			attribute boolean update;      /* enable update (default is FALSE) */
			attribute boolean downgrade;   /* enable update (default is FALSE) */
			attribute string package_list; /* Local package list to use, rather then download 
							  from server */

			attribute string server;  /* server to use for both file and package list download */
			attribute string username; 
			attribute string cgi;
			attribute boolean auth; 
			attribute long server_port;   /*  */

			attribute string log_file;  /* where to put the logfile (otherwise it blurps to stdout */
			attribute string tmp_dir;   /* directory to store tmp files in (/tmp/eazel-install) */

			attribute ProtocolEnum protocol; /* */
			
			attribute boolean ssl_rename; /* Do the renaming trick, where all hostnames in 
							 all urls are renamed to localhost. This makes
							 ssl tunneling work */
			attribute boolean ignore_file_conflicts; /* instruct the installer to not
								    do file conflict checking */

			attribute boolean ei2;

			/* Install/uninstall using the packagelist from the server */
			oneway void install (in string packagelist, in InstallCallback cb);
			oneway void uninstall (in string packagelist, in InstallCallback cb);

			/* These are for installing/uninstalling specific packages */
			oneway void install_packages (in CategoryStructList categories, 
						      in string root,
						      in InstallCallback cb);
			oneway void uninstall_packages (in CategoryStructList categories, 
							in string root,
							in InstallCallback cb);

			boolean query_server (in PackageDataStruct q, 
					      out PackageDataStruct result);

			/* Run a query to the package sys */
			PackageDataStructList simple_query (in string query,
							    in string root);

			/* Revert a operation */
			oneway void revert_transaction (in string xml, 
							in string root,
							in InstallCallback cb);

			/* Cancel an ongoing download. */
			void stop ();

		        /* Delete the temp dir and its contents, after an operation. */
		        oneway void delete_files ();
                };
	};
};
};
